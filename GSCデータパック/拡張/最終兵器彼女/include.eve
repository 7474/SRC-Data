
###### ちせ暴走インクル #####

*破壊 ちせ(最終兵器彼女):
If Unit(ちせ(最終兵器彼女)) = 最終兵器・ちせ or _
Unit(ちせ(最終兵器彼女)) = 最終兵器・ちせ(巨大) Then

Switch Random(3)
Case 1
AutoTalk ちせ(最終兵器彼女)(暴走) X(ちせ(最終兵器彼女)) Y(ちせ(最終兵器彼女))
いやぁぁぁぁぁっ！！
End
Case 2
AutoTalk ちせ(最終兵器彼女) X(ちせ(最終兵器彼女)) Y(ちせ(最終兵器彼女))
あっ…、だめ…っ、シュウちゃん…、ああっ！！
End
Case 3
AutoTalk ちせ(最終兵器彼女) X(ちせ(最終兵器彼女)) Y(ちせ(最終兵器彼女))
あれ…？ あ…、あれ？ あ…れ？
AutoTalk ちせ(最終兵器彼女)(暴走) X(ちせ(最終兵器彼女)) Y(ちせ(最終兵器彼女))
…あ
End
Endsw

Center ちせ(最終兵器彼女)

	If Unit(ちせ(最終兵器彼女)) = 最終兵器・ちせ Then
	Upgrade 最終兵器・ちせ 最終兵器・ちせ(暴走)
	Elseif Unit(ちせ(最終兵器彼女)) = 最終兵器・ちせ(巨大) Then
	Upgrade 最終兵器・ちせ(巨大) 最終兵器・ちせ(暴走)(巨大)
	Endif

PlayEffect 大光柱 ちせ(最終兵器彼女)
Endif

Exit


*エピローグ:
If Unit(ちせ(最終兵器彼女)) = 最終兵器・ちせ(暴走) Then
Upgrade 最終兵器・ちせ(暴走) 最終兵器・ちせ
	If Status(ちせ(最終兵器彼女)) = 出撃 Then
	Escape ちせ(最終兵器彼女)
	Endif
Destroy ちせ(最終兵器彼女)

Elseif Unit(ちせ(最終兵器彼女)) = 最終兵器・ちせ(暴走)(巨大) Then
Upgrade 最終兵器・ちせ(暴走)(巨大) 最終兵器・ちせ(巨大)
	If Status(ちせ(最終兵器彼女)) = 出撃 Then
	Escape ちせ(最終兵器彼女)
	Endif

Destroy ちせ(最終兵器彼女)
Endif

Exit

# 暴走状態を解除。暴走した時点で修理費を取られる。


*破壊 ちせ(最終兵器彼女)(暴走):
Set 暴走ちせ破壊フラグ
Exit

*使用後 対象パイロット 祈り:
If 暴走ちせ破壊フラグ Then
	If Status(ちせ(最終兵器彼女)) = 出撃 and _
	Unit(ちせ(最終兵器彼女)) = 最終兵器・ちせ(暴走) Then
	Upgrade 最終兵器・ちせ(暴走) 最終兵器・ちせ
	Unset 暴走ちせ破壊フラグ

	Elseif Status(ちせ(最終兵器彼女)) = 出撃 and _
	Unit(ちせ(最終兵器彼女)) = 最終兵器・ちせ(暴走)(巨大) Then
	Upgrade 最終兵器・ちせ(暴走)(巨大) 最終兵器・ちせ(巨大)
	Unset 暴走ちせ破壊フラグ
	Endif
Endif
Exit

# 祈りで復活させた場合に、暴走状態を解除。
# 「祈りで復活させたパイロット」は関数で直接習得できないので、
# 「ちせが破壊された状態で、祈り使用後に暴走ちせが存在しているかどうか」で判別させる。
