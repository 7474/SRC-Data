
## ＳＰ関連ルーチン ##

## 報復

*攻撃 全 全:
If (SpecialPower(相手ユニットＩＤ,報復) + SpecialPower(対象ユニットＩＤ,報復)) = 0 Exit
If SpecialPower(相手ユニットＩＤ,報復) = 1 Then
Set ＳＰ報復_相手ＨＰ[相手ユニットＩＤ] HP(相手ユニットＩＤ)
Endif
If SpecialPower(対象ユニットＩＤ,報復) = 1 Then
Set ＳＰ報復_相手ＨＰ[対象ユニットＩＤ] HP(対象ユニットＩＤ)
Endif
Exit

*攻撃後 全 全:
Local ＳＰ報復_ＨＰ補正 ＳＰ報復_攻撃者 ＳＰ報復_防御者 ＳＰ報復_消費霊力 ＳＰ報復_攻撃者現ＨＰ ＳＰ報復_攻撃者元ＨＰ ＳＰ報復_防御者元ＨＰ

If (SpecialPower(相手ユニットＩＤ,報復) + SpecialPower(対象ユニットＩＤ,報復)) = 0 Then
UnSet ＳＰ報復_相手ＨＰ
Exit
Endif

If ((Info(ユニット,相手ユニットＩＤ,武器,相手ユニット使用武器,属性所有,霊) + Info(ユニット,相手ユニットＩＤ,武器,相手ユニット使用武器,属性所有,プ)) _
* Info(ユニット,相手ユニットＩＤ,特殊能力所有,霊力変換器)) > 0 Then
If Plana(相手ユニットＩＤ) < Info(ユニット,相手ユニットＩＤ,特殊能力レベル,霊力変換器) Then
Set ＳＰ報復_消費霊力[防御者] Min((Max(Info(ユニット,相手ユニットＩＤ,武器,相手ユニット使用武器,属性レベル,霊),_
Info(ユニット,相手ユニットＩＤ,武器,相手ユニット使用武器,属性レベル,プ)) * 5),(Info(ユニット,相手ユニットＩＤ,特殊能力レベル,霊力変換器) - Plana(相手ユニットＩＤ))) 
Incr ＳＰ報復_ＨＰ補正[防御者] (ＳＰ報復_消費霊力[防御者] * 10 * (ＳＰ報復_相手ＨＰ[相手ユニットＩＤ] / (Info(ユニット,相手ユニットＩＤ,最大ＨＰ))))
Endif
Endif

If ((Info(ユニット,対象ユニットＩＤ,武器,対象ユニット使用武器,属性所有,霊) + Info(ユニット,対象ユニットＩＤ,武器,対象ユニット使用武器,属性所有,プ)) _
* Info(ユニット,対象ユニットＩＤ,特殊能力所有,霊力変換器)) > 0 Then
If Plana(対象ユニットＩＤ) < Info(ユニット,対象ユニットＩＤ,特殊能力レベル,霊力変換器) Then
Set ＳＰ報復_消費霊力[攻撃者] Min((Max(Info(ユニット,対象ユニットＩＤ,武器,対象ユニット使用武器,属性レベル,霊),_
Info(ユニット,対象ユニットＩＤ,武器,対象ユニット使用武器,属性レベル,プ)) * 5),(Info(ユニット,対象ユニットＩＤ,特殊能力レベル,霊力変換器) - Plana(対象ユニットＩＤ))) 
Incr ＳＰ報復_ＨＰ補正[攻撃者] (ＳＰ報復_消費霊力[攻撃者] * 10 * (ＳＰ報復_相手ＨＰ[対象ユニットＩＤ] / (Info(ユニット,対象ユニットＩＤ,最大ＨＰ))))
Endif
Endif

If Info(ユニット,相手ユニットＩＤ,武器,相手ユニット使用武器,属性所有,失) = 1 Then
Incr ＳＰ報復_ＨＰ補正[防御者] (Info(ユニット,相手ユニットＩＤ,武器,相手ユニット使用武器,属性レベル,失) * Info(ユニット,相手ユニットＩＤ,最大ＨＰ) * 0.1)
Endif
If Info(ユニット,対象ユニットＩＤ,武器,対象ユニット使用武器,属性所有,失) = 1 Then
Incr ＳＰ報復_ＨＰ補正[攻撃者] (Info(ユニット,対象ユニットＩＤ,武器,対象ユニット使用武器,属性レベル,失) * Info(ユニット,対象ユニットＩＤ,最大ＨＰ) * 0.1)
Endif


If (SpecialPower(相手ユニットＩＤ,報復) + SpecialPower(対象ユニットＩＤ,報復)) = 2 Then
Set ＳＰ報復_攻撃者 対象ユニットＩＤ
Set ＳＰ報復_防御者 相手ユニットＩＤ
Set ＳＰ報復_攻撃者元ＨＰ ＳＰ報復_相手ＨＰ[対象ユニットＩＤ]
Set ＳＰ報復_攻撃者現ＨＰ HP(対象ユニットＩＤ)
Set ＳＰ報復_防御者元ＨＰ ＳＰ報復_相手ＨＰ[相手ユニットＩＤ]
Unset ＳＰ報復_相手ＨＰ

If (HP(ＳＰ報復_防御者) + ＳＰ報復_ＨＰ補正[防御者] + 1) < ＳＰ報復_防御者元ＨＰ Then
ClearSpecialPower ＳＰ報復_防御者 報復
Call ＳＰ報復メインルーチン ＳＰ報復_攻撃者 _
(ＳＰ報復_防御者元ＨＰ - ＳＰ報復_ＨＰ補正[防御者] - HP(ＳＰ報復_防御者))
Endif
If (Status(ＳＰ報復_攻撃者) = 出撃) And _
((ＳＰ報復_攻撃者現ＨＰ + ＳＰ報復_ＨＰ補正[攻撃者] + 1) < ＳＰ報復_攻撃者元ＨＰ) Then
ClearSpecialPower ＳＰ報復_攻撃者 報復
Call ＳＰ報復メインルーチン ＳＰ報復_防御者 _
(ＳＰ報復_攻撃者元ＨＰ - ＳＰ報復_ＨＰ補正[攻撃者] - ＳＰ報復_攻撃者現ＨＰ)
Endif
Exit
Endif

If SpecialPower(相手ユニットＩＤ,報復) = 1 Then
If (HP(相手ユニットＩＤ) + ＳＰ報復_ＨＰ補正[防御者] + 1) < ＳＰ報復_相手ＨＰ[相手ユニットＩＤ] Then
ClearSpecialPower 相手ユニットＩＤ 報復
Call ＳＰ報復メインルーチン 対象ユニットＩＤ _
(ＳＰ報復_相手ＨＰ[相手ユニットＩＤ] - HP(相手ユニットＩＤ) - ＳＰ報復_ＨＰ補正[防御者])
Exit
Endif
Endif

If SpecialPower(対象ユニットＩＤ,報復) = 1 Then
If (HP(対象ユニットＩＤ) + ＳＰ報復_ＨＰ補正[攻撃者] + 1) < ＳＰ報復_相手ＨＰ[対象ユニットＩＤ] Then
ClearSpecialPower 対象ユニットＩＤ 報復
Call ＳＰ報復メインルーチン 相手ユニットＩＤ _
(ＳＰ報復_相手ＨＰ[対象ユニットＩＤ] - HP(対象ユニットＩＤ) - ＳＰ報復_ＨＰ補正[攻撃者])
Exit
Endif
Endif
Unset ＳＰ報復_相手ＨＰ
Exit



ＳＰ報復メインルーチン:
Local ＳＰ報復_ダメージ
Set ＳＰ報復_ダメージ Int((Args(2) * 1.5))
If HP(Args(1)) < (ＳＰ報復_ダメージ + 10) Then
Set ＳＰ報復_ダメージ (HP(Args(1)) - 10)
Endif
If ＳＰ報復_ダメージ < 0 Then
Set ＳＰ報復_ダメージ 0
Endif
Center X(Args(1)) Y(Args(1))
Explode M X(Args(1)) Y(Args(1))
AutoTalk システム
$(Nickname(Args(1)))は$(ＳＰ報復_ダメージ)の報復ダメージを受けた。
End
HP(Args(1)) = (HP(Args(1)) - ＳＰ報復_ダメージ)
UnSet ＳＰ報復_相手ＨＰ
Return

報復ダミー削除:
ClearSpecialPower 対象ユニットＩＤ 報復ダミー
Return


## 我慢

*攻撃 全 全:
If SpecialPower(対象ユニットＩＤ,我慢) Then
ClearSpecialPower 対象ユニットＩＤ 我慢
SpecialPower 対象ユニットＩＤ 我慢ダミー
EndIf
If SpecialPower(相手ユニットＩＤ,我慢) Then
ClearSpecialPower 相手ユニットＩＤ 我慢
SpecialPower 相手ユニットＩＤ 我慢ダミー
EndIf
Exit

*行動終了 全:
If SpecialPower(対象ユニットＩＤ,我慢ダミー) Then
ClearSpecialPower 対象ユニットＩＤ 我慢ダミー
EndIf
If SpecialPower(相手ユニットＩＤ,我慢ダミー) Then
ClearSpecialPower 相手ユニットＩＤ 我慢ダミー
EndIf
Exit
