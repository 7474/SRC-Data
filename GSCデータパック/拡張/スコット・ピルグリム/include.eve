
# ローカル戦闘アニメです。

# SCOTT_コンボ
# SCOTT_バックスタブ
# SCOTT_高速準備
# SCOTT_回転連続描画
# SCOTT_二重連続描画
# SCOTT_バックジャンプ
# SCOTT_スーパーキック改
# SCOTT_背向け



#_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/


戦闘アニメ_SCOTT_コンボ命中:

_CenteringCheck 相手ユニットＩＤ
Local i j X_P Y_P ANG
Local C_X = WX(相手ユニットＩＤ)
Local C_Y = WY(相手ユニットＩＤ)
Local B_C = _G_BC_FA()

Local UID = _GetName(対象ユニットＩＤ,_GetIDFromArgs())
Local C_X2 = (WX(UID) - 8)
Local C_Y2 = (WY(UID) - 8)
Local ANG2 = _GetAngle(UID)

Local k = 23
Font 16pt Italic Bold

Font #FF5500

For i = 1 To 20
  Set ANG Random(360)
  Set X_P (Random(12) + C_X - 6)
  Set Y_P (Random(12) + C_Y - 6)

  Incr j 1
	If j > 7 Then
		j = 1
	Endif
  Incr k 1

  Wait Start
  PlaySound FightHit(3).wav
  ClearPicture
  PaintPicture ("Hit\EFFECT_Hit(Orange)01.bmp") X_P Y_P 24 24 右回転 ANG 透過
  PaintPicture ("Attack\EFFECT_Stick0" & j & ".bmp") C_X2 C_Y2 右回転 ANG2 透過
  Font #444444
  PaintString (C_X + 18) (C_Y - 15) (k & "ＨＩＴＳ！")
  Font #FF5555
  PaintString (C_X + 16) (C_Y - 16) (k & "ＨＩＴＳ！")
  Refresh
  Wait Until 0.5
  Incr j 1
  Incr k 1
  ClearPicture
  PaintPicture ("Hit\EFFECT_Hit(Yellow)01.bmp") X_P Y_P 36 36 右回転 ANG 透過
  PaintPicture ("Attack\EFFECT_Stick0" & j & ".bmp") C_X2 C_Y2 右回転 ANG2 透過
  Font #444444
  PaintString (C_X + 18) (C_Y - 15) (k & "ＨＩＴＳ！")
  Font #FF5555
  PaintString (C_X + 16) (C_Y - 16) (k & "ＨＩＴＳ！")
  Refresh
  Wait Until 1.0
Next


# とどめアッパー

Incr C_X2 8
Incr C_Y2 8
Local BMP_FILE = _GetBMPName(UID)
Local BMP_FILE2 = _GetTileName(UID)
Local X_F = _GetCircumference(ANG2,Ｘ)
Local Y_F = _GetCircumference(ANG2,Ｙ)
Local DRCT
Local T_COL = _ColorTime()

If (X(UID) < _Get_X(相手ユニットＩＤ)) Then
  Set DRCT 左右反転
Else
  Set DRCT ""
EndIf

For i = 1 To 6
  ClearPicture
  _PaintTile BMP_FILE2 C_X2 C_Y2
  Switch i
  Case 1
    PaintPicture BMP_FILE (C_X2 + (X_F * -10)) (C_Y2 + (Y_F * -10)) 透過 DRCT T_COL
    Font #444444
    PaintString (C_X + 18) (C_Y - 15) (k & "ＨＩＴＳ！")
    Font #FF5555
    PaintString (C_X + 16) (C_Y - 16) (k & "ＨＩＴＳ！")
  Case 2
    PaintPicture BMP_FILE (C_X2 + (X_F * -15)) (C_Y2 + (Y_F * -15)) 透過 DRCT T_COL
    Font #444444
    PaintString (C_X + 18) (C_Y - 15) (k & "ＨＩＴＳ！")
    Font #FF5555
    PaintString (C_X + 16) (C_Y - 16) (k & "ＨＩＴＳ！")
  Case 3
    PaintPicture BMP_FILE (C_X2 + (X_F * -18)) (C_Y2 + (Y_F * -18)) 透過 DRCT T_COL
    PlaySound Swing.wav
    PaintPicture (Slash\EFFECT_HyperSlashLine(Red)01.bmp) (C_X2 - 4 + (X_F * -18)) (C_Y2 - 4 + (Y_F * -18)) 40 40 透過 DRCT 上下反転 T_COL
    Font #444444
    PaintString (C_X + 18) (C_Y - 15) (k & "ＨＩＴＳ！")
    Font #FF5555
    PaintString (C_X + 16) (C_Y - 16) (k & "ＨＩＴＳ！")
  Case 4
    PaintPicture BMP_FILE (C_X2 + (X_F * -10)) (C_Y2 + (Y_F * -10)) 透過 DRCT T_COL
    PaintPicture (Slash\EFFECT_HyperSlashLine(Red)02.bmp) (C_X2 - 4 + (X_F * -10)) (C_Y2 - 4 + (Y_F * -10)) 40 40 透過 DRCT 上下反転 T_COL
    Font #444444
    PaintString (C_X + 18) (C_Y - 15) (k & "ＨＩＴＳ！")
    Font #FF5555
    PaintString (C_X + 16) (C_Y - 16) (k & "ＨＩＴＳ！")
  Case 5
    PaintPicture BMP_FILE C_X2 C_Y2 透過 DRCT T_COL
    PaintPicture (Slash\EFFECT_HyperSlashLine(Red)03.bmp) (C_X2 - 4 + (X_F * -2)) (C_Y2 - 4 + (Y_F * -2)) 40 40 透過 DRCT 上下反転 T_COL
    Font #444444
    PaintString (C_X + 18) (C_Y - 15) (k & "ＨＩＴＳ！")
    Font #FF5555
    PaintString (C_X + 16) (C_Y - 16) (k & "ＨＩＴＳ！")
  Case 6
    PaintPicture BMP_FILE (C_X2 + (X_F * 10)) (C_Y2 + (Y_F * 10)) 透過 DRCT T_COL
    PaintPicture (Slash\EFFECT_HyperSlashLine(Red)04.bmp) (C_X2 - 4 + (X_F * 6)) (C_Y2 - 4 + (Y_F * 6)) 40 40 透過 DRCT 上下反転 T_COL
    Font #444444
    PaintString (C_X + 18) (C_Y - 15) (k & "ＨＩＴＳ！")
    Font #FF5555
    PaintString (C_X + 16) (C_Y - 16) (k & "ＨＩＴＳ！")
  EndSw
  Refresh
  Wait Until (i * 0.4)
Next


# 強打

Set BMP_FILE _GetBMPName(相手ユニットＩＤ)
Set BMP_FILE2 _GetTileName(相手ユニットＩＤ)
Set ANG _GetAngle(UID)
Set X_F _GetCircumference(ANG,Ｘ)
Set Y_F _GetCircumference(ANG,Ｙ)

Wait Start
_SelectSound_HR "BreakOff.wav" "Explode.wav"
ChangeUnitBitmap 相手ユニットＩＤ 非表示
Incr k 1

For i = 1 To 4
  ClearPicture
  _PaintTile BMP_FILE2 C_X C_Y 
  Switch i
  Case 1
    PaintPicture BMP_FILE C_X C_Y 透過 T_COL
    PaintPicture "Hit\EFFECT_HIT01.bmp" (C_X + 4) (C_Y + 4) 24 24 透過 T_COL
    Font 18pt #444444
    PaintString (C_X + 18) (C_Y - 15) (k & "ＨＩＴＳ！")
    Font #FFAA77
    PaintString (C_X + 16) (C_Y - 16) (k & "ＨＩＴＳ！")
  Case 2
    PaintPicture BMP_FILE (C_X + (X_F * 10)) (C_Y + (Y_F * 10)) 透過 T_COL
    PaintPicture "Hit\EFFECT_HIT01.bmp" C_X C_Y 透過 T_COL
    Font 24pt #444444
    PaintString (C_X + 18) (C_Y - 15) (k & "ＨＩＴＳ！")
    Font #FFDD77
    PaintString (C_X + 16) (C_Y - 16) (k & "ＨＩＴＳ！")
  Case 3
    PaintPicture BMP_FILE (C_X + (X_F * 15)) (C_Y + (Y_F * 15)) 透過 T_COL
    Font #444444
    PaintString (C_X + 18) (C_Y - 15) (k & "ＨＩＴＳ！")
    Font #FFDD77
    PaintString (C_X + 16) (C_Y - 16) (k & "ＨＩＴＳ！")
  Case 4
    PaintPicture BMP_FILE (C_X + (X_F * 18)) (C_Y + (Y_F * 18)) 透過 T_COL
    Font 16pt #444444
    PaintString (C_X + 18) (C_Y - 15) (k & "ＨＩＴＳ！")
    Font #FF7777
    PaintString (C_X + 16) (C_Y - 16) (k & "ＨＩＴＳ！")
  EndSw
  Refresh
  Wait Until (i * 0.7)
Next
ChangeUnitBitmap 相手ユニットＩＤ 非表示解除
ClearPicture
Refresh
Return


ClearPicture
Refresh

Return


#_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/


戦闘アニメ_SCOTT_バックスタブ命中:
_CenteringCheck 相手ユニットＩＤ

Local BMP_FILE = _GetBMPName(対象ユニットＩＤ)
Local BMP_FILE2 = _GetBMPName(相手ユニットＩＤ)
Local BMP_FILE3 = _GetTileName(相手ユニットＩＤ)
Local C_X = WX(相手ユニットＩＤ)
Local C_Y = WY(相手ユニットＩＤ)
Local i

PaintPicture Common\EFFECT_AccidentalImage01.bmp (C_X + 8) C_Y 32 32 透過
PaintPicture BMP_FILE2 C_X C_Y - - 透過
Refresh
Wait 2

ClearPicture
PaintPicture BMP_FILE3 C_X C_Y - - 
PaintPicture BMP_FILE (C_X + 8) C_Y 32 32 透過 暗
PaintPicture BMP_FILE2 C_X C_Y - - 透過
Refresh
Wait 1

PlaySound Stab.wav
ClearPicture
PaintPicture BMP_FILE3 C_X C_Y - - 
PaintPicture BMP_FILE (C_X + 8) C_Y 32 32 透過 暗
PaintPicture BMP_FILE2 (C_X - 2) (C_Y - 4) - - 透過
PaintPicture BeamWeapon\EFFECT_BeamSword(Black)06.bmp (C_X - 8) (C_Y - 10) 28 28 透過
Refresh
Wait 1

Wait Start

For i = 1 to 7
	ClearPicture
	PaintPicture BMP_FILE3 C_X C_Y - - 
	PaintPicture BMP_FILE (C_X + 8) C_Y 32 32 透過 暗
	PaintPicture BMP_FILE2 (C_X - 2) (C_Y - 4) - - 透過
	PaintPicture ("Requid\EFFECT_Blood(Red)0" & i & ".bmp") (C_X - 32) (C_Y - 32) 48 48 透過
	PaintPicture BeamWeapon\EFFECT_BeamSword(Black)06.bmp (C_X - 8) (C_Y - 10) 28 28 透過
	Refresh
	Wait Until (i * 0.3)
Next
Wait 3

ClearPicture
Refresh

Return




#_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/


戦闘アニメ_SCOTT_高速準備準備:
戦闘アニメ_SCOTT_高速準備攻撃:
戦闘アニメ_SCOTT_高速準備命中:

# 引数 待機=(整数) 武器を構えた後のWait
# 他、振る武器準備と同様

UpVar

Local UID = _GetName(対象ユニットＩＤ,_GetIDFromArgs())
_CenteringCheck UID
Local C_X = WX(UID)
Local C_Y = WY(UID)
Local WP_FILE = _GetBmpFromArgs()
Local G_S = _GetPicSizeFromArgs()
Local X_P
Local T_COL_F = _ColorTime(_IsLight())
Local WA = _GetOptValueFromArgs("待機")

Set X_P (16 - (G_S \ 2))
Incr C_Y (32 - G_S)

_SelectSound "-.wav" _GetWavFromArgs()

If (_IsDoubleWeapon() = 1) Then
  Wait Start
  ClearPicture
  PaintPicture WP_FILE (C_X - 12 + X_P) (C_Y + 4) G_S G_S 透過 T_COL_F
  PaintPicture WP_FILE (C_X + 12 + X_P) (C_Y + 4) G_S G_S 透過 左右反転 T_COL_F
  Refresh
  Wait Until 1
  ClearPicture
  PaintPicture WP_FILE (C_X - 20 + X_P) (C_Y - 4) G_S G_S 透過 T_COL_F
  PaintPicture WP_FILE (C_X + 20 + X_P) (C_Y - 4) G_S G_S 透過 左右反転 T_COL_F
  Refresh
  Wait Until 2
  ClearPicture
  PaintPicture WP_FILE (C_X - 16 + X_P) C_Y G_S G_S 透過 T_COL_F
  PaintPicture WP_FILE (C_X + 16 + X_P) C_Y G_S G_S 透過 左右反転 T_COL_F
  Refresh
  Wait Until 3.5
Else
  If (X(UID) >= _Get_X(相手ユニットＩＤ)) Then
    Wait Start
    ClearPicture
    PaintPicture WP_FILE (C_X - 12 + X_P) (C_Y + 4) G_S G_S 透過 T_COL_F
    Refresh
    Wait Until 1
    ClearPicture
    PaintPicture WP_FILE (C_X - 20 + X_P) (C_Y - 4) G_S G_S 透過 T_COL_F
    Refresh
    Wait Until 2
    ClearPicture
    PaintPicture WP_FILE (C_X - 16 + X_P) C_Y G_S G_S 透過 T_COL_F
    Refresh
    Wait Until 3.5
  Else
    Wait Start
    ClearPicture
    PaintPicture WP_FILE (C_X + 12 + X_P) (C_Y + 4) G_S G_S 透過 左右反転 T_COL_F
    Refresh
    Wait Until 1
    ClearPicture
    PaintPicture WP_FILE (C_X + 20 + X_P) (C_Y - 4) G_S G_S 透過 左右反転 T_COL_F
    Refresh
    Wait Until 2
    ClearPicture
    PaintPicture WP_FILE (C_X + 16 + X_P) C_Y G_S G_S 透過 左右反転 T_COL_F
    Refresh
    Wait Until 3.5
  EndIf
EndIf

Wait Until WA

Return "Keep"


#_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/


戦闘アニメ_SCOTT_回転連続描画準備:
戦闘アニメ_SCOTT_回転連続描画攻撃:
戦闘アニメ_SCOTT_回転連続描画命中:
戦闘アニメ_SCOTT_回転連続描画使用:
戦闘アニメ_SCOTT_回転連続描画クリティカル:
# 引数１ 画像ファイル(連続表示)
# 引数２ 画像サイズ
# 引数３ 表示時間
# 引数４ 表示位置
# 引数５ 効果音
# 引数６ 発光
# 引数７ 保持
# 引数８ 方向判定
# 引数９ ユニットＩＤ
#
# 引数 角度=(整数) (指定した角度に右周りで画像を回転させる。「発射」と組み合わせ可能)
# 引数 ランダム (ランダム角度で画像を回転) or 発射(発射画像を相手方向に向けて描画)
# 引数 中間 対象と相手ユニットの中間でアニメを再生する


Local UID = _GetName(対象ユニットＩＤ,_GetIDFromArgs())
_CenteringCheck UID

Local GS_STR = _GetBoxSizeFromArgs(Args(2))
Local G_SX = _GN(32,_SelectBmpSizeType(GS_STR,"Ｘ"))
Local G_SY = _GN(32,_SelectBmpSizeType(GS_STR,"Ｙ"))
Local i
Local j
Local C_X = WX(UID)
LOcal C_Y = WY(UID)
Local G_X = (WX(UID) + 16)
LOcal G_Y = (WY(UID) + 16)
Local BMP_FILE = _GetBMPName(UID)
Local T_COL_F = _ColorTime(_IsLight())
Local W_T
Local B_FLG = _S_OPT_FA("背面")
Local DRCT = ""
Local POS = _GetDrawPosFromArgs()
Local T_COL = _ColorTime()

Local ANG = 0
Local ANGP

If ((X(対象ユニットＩＤ) < _Get_X(相手ユニットＩＤ)) And (_S_OPT_FA("方向判定") = "方向判定")) Then
  Set DRCT 左右反転
EndIf
Local UD_OPT = _S_OPT_FA("上下反転")
Local LR_OPT = _S_OPT_FA("左右反転")
If ((DRCT != "") And (LR_OPT != "")) Then
  Set DRCT ""
ElseIf (LR_OPT != "") Then
  Set DRCT LR_OPT
EndIf

If (_SearchOptionFromArgs("中間") != "") Then
  G_X = (((WX(対象ユニットＩＤ) + WX(相手ユニットＩＤ)) * 0.5) + 16)
  G_Y = (((WY(対象ユニットＩＤ) + WY(相手ユニットＩＤ)) * 0.5) + 16)
EndIf


Incr G_X (G_SX * -0.5)

Switch POS
Case "上辺"
  Incr G_Y -16
Case "下辺"
  Incr G_Y ((G_SY * -1) + 16)
Case Else
  Incr G_Y (G_SY * -0.5)
EndSw

# 角度設定

If (_SearchOptionFromArgs("発射") != "") Then
  ANG = _GetAngle(対象ユニットＩＤ)
EndIf

If (DRCT = "左右反転") Then
	ANGP = (_GetOptValueFromArgs("角度") * -1)
Else
	ANGP = _GetOptValueFromArgs("角度")
EndIf

ANG = ANG + ANGP




_GBA_GetBmpPatternInfo _G_APTN_FA() _GetName(_GetWavFromArgs(),_GetOptParamsFromArgs("連続効果音"))
Set W_T (Args(3) / _GBA_P_TTL)

#_GetNumberFromArgs(Args(4))

For j = 1 To _GetNumberFromArgs(Args(4))

If (_SearchOptionFromArgs("ランダム") != "") Then
  ANG = random(360)
EndIf

Wait Start
	For i = 1 To _GBA_P_TTL
	  _CheckPlaySound _GBA_P_WAV[i]
	  ClearPicture
	  If (B_FLG = "背面") Then
	    PaintPicture _SetBmpNum2(_GBA_P_NAME,(_GBA_P_STR + ((i - 1) * _GBA_P_FRC)),_GBA_P_TYPE) _
	                 G_X G_Y G_SX G_SY DRCT 透過 T_COL_F UD_OPT LR_OPT 右回転 ANG
	    PaintPicture BMP_FILE C_X C_Y 32 32 透過 T_COL
	  Else
	    PaintPicture _SetBmpNum2(_GBA_P_NAME,(_GBA_P_STR + ((i - 1) * _GBA_P_FRC)),_GBA_P_TYPE) _
	                 G_X G_Y G_SX G_SY DRCT 透過 T_COL_F UD_OPT LR_OPT 右回転 ANG
	  EndIf
	  Refresh
	  Wait Until (i * W_T)
	Next
Next

_GBA_UnSetBmpPattern

If (_S_OPT_FA("保持") = "保持") Then
  Return "Keep"
Else
  Return
EndIf


#_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/



戦闘アニメ_SCOTT_二重連続描画準備:
戦闘アニメ_SCOTT_二重連続描画攻撃:
戦闘アニメ_SCOTT_二重連続描画命中:
戦闘アニメ_SCOTT_二重連続描画使用:
戦闘アニメ_SCOTT_二重連続描画クリティカル:
# 連番形式画像１ サイズ 連番形式画像２ サイズ 再生時間 回数
# ファイル１と２は同ペースで再生される。速度と枚数は１枚目から得られる情報に倣う
#
# 引数１ 画像ファイル(連続表示)
# 引数２ 画像サイズ
# 引数３ 表示時間
# 引数４ 表示位置
# 引数５ 効果音
# 引数６ 発光
# 引数７ 保持
# 引数８ 方向判定
# 引数９ ユニットＩＤ
#
# 引数 角度=(整数) 角度２(指定した角度に右周りで画像を回転させる。「発射」と組み合わせ可能)
# 引数 ランダム、ランダム２ (ランダム角度で画像を回転) or 発射(発射画像を相手方向に向けて描画)
# 引数 中間 対象と相手ユニットの中間でアニメを再生する
# 引数 上辺、上辺２、下辺、下辺２
# 引数 上下反転 上下反転２ 左右反転 左右反転２

Local UID = _GetName(対象ユニットＩＤ,_GetIDFromArgs())
_CenteringCheck UID

Local GS_STR = _GetBoxSizeFromArgs(Args(2))
Local GS_STR2 = _GetBoxSizeFromArgs(Args(4))
Local G_SX = _GN(32,_SelectBmpSizeType(GS_STR,"Ｘ"))
Local G_SY = _GN(32,_SelectBmpSizeType(GS_STR,"Ｙ"))
Local G_SX2 = _GN(32,_SelectBmpSizeType(GS_STR2,"Ｘ"))
Local G_SY2 = _GN(32,_SelectBmpSizeType(GS_STR2,"Ｙ"))
Local i j k
Local C_X = WX(UID)
LOcal C_Y = WY(UID)
Local G_X = (WX(UID) + 16)
LOcal G_Y = (WY(UID) + 16)
Local BMP_FILE = _GetBMPName(UID)
Local T_COL_F = _ColorTime(_IsLight())
Local W_T
Local B_FLG = _S_OPT_FA("背面")
Local DRCT = ""
Local DRCT2 = ""
Local POS = _GetDrawPosFromArgs()
Local T_COL = _ColorTime()

Local ANG = 0

# 画像反転判定

If ((X(対象ユニットＩＤ) < _Get_X(相手ユニットＩＤ)) And (_S_OPT_FA("方向判定") = "方向判定")) Then
  Set DRCT 左右反転
  Set DRCT2 左右反転
EndIf
Local UD_OPT = _S_OPT_FA("上下反転")
Local LR_OPT = _S_OPT_FA("左右反転")
If ((DRCT != "") And (LR_OPT != "")) Then
  Set DRCT ""
ElseIf (LR_OPT != "") Then
  Set DRCT LR_OPT
EndIf
Local UD_OPT2 = ""
If (_SearchOptionFromArgs("上下反転２") != "") Then
	UD_OPT2 = "上下反転"
EndIf
Local LR_OPT2 = ""
If (_SearchOptionFromArgs("左右反転２") != "") Then
	LR_OPT2 = "左右反転"
Endif
If ((DRCT2 != "") And (LR_OPT2 != "")) Then
  Set DRCT2 ""
ElseIf (LR_OPT != "") Then
  Set DRCT2 LR_OPT2
EndIf


If (_SearchOptionFromArgs("中間") != "") Then
  G_X = (((WX(対象ユニットＩＤ) + WX(相手ユニットＩＤ)) * 0.5) + 16)
  G_Y = (((WY(対象ユニットＩＤ) + WY(相手ユニットＩＤ)) * 0.5) + 16)
EndIf

Local G_X2 = G_X
LOcal G_Y2 = G_Y

Incr G_X (G_SX * -0.5)
Incr G_X2 (G_SX2 * -0.5)

Switch POS
Case "上辺"
  Incr G_Y -16
Case "下辺"
  Incr G_Y ((G_SY * -1) + 16)
Case Else
  Incr G_Y (G_SY * -0.5)
EndSw

If (_SearchOptionFromArgs("上辺２") != "") Then
  Incr G_Y2 -16
elseIf (_SearchOptionFromArgs("下辺２") != "") Then
  Incr G_Y2 ((G_SY2 * -1) + 16)
Else
  Incr G_Y2 (G_SY2 * -0.5)
EndIf

# 角度設定

If (_SearchOptionFromArgs("発射") != "") Then
  ANG = _GetAngle(対象ユニットＩＤ)
EndIf

Local ANG2 = ANG

ANG = ANG + _GetOptValueFromArgs("角度")
ANG2 = ANG2 + _GetOptValueFromArgs("角度２")

If (DRCT = "左右反転") Then
	ANG = ANG * -1
	ANG2 = ANG2 * -1
EndIf



# 先に二枚目ファイル画像情報取得
_GBA_GetBmpPatternInfo _G_APTN_FA(Args(3)) _GetName(_GetWavFromArgs(),_GetOptParamsFromArgs("連続効果音"))
# ２枚目の情報を保存
Local _GBA_P_NAME2 = _GBA_P_NAME
Local _GBA_P_STR2 = _GBA_P_STR
Local _GBA_P_END2 = _GBA_P_END
Local _GBA_P_TYPE2 = _GBA_P_TYPE
Local _GBA_P_FRC2 = _GBA_P_FRC
Local _GBA_P_TTL2 = _GBA_P_TTL

# 一枚目情報取得
_GBA_GetBmpPatternInfo _G_APTN_FA(Args(1)) _GetName(_GetWavFromArgs(),_GetOptParamsFromArgs("連続効果音"))

Set W_T (Args(5) / _GBA_P_TTL)

#_GetNumberFromArgs(Args(4))

For j = 1 To _GetNumberFromArgs(Args(6))

If (_SearchOptionFromArgs("ランダム") != "") Then
  ANG = random(360)
EndIf
If (_SearchOptionFromArgs("ランダム２") != "") Then
  ANG2 = random(360)
EndIf

Set k 0

Wait Start
	For i = 1 To _GBA_P_TTL
	
	if i > _GBA_P_TTL2 Then
	else
		Incr k 1
	Endif
	
	  _CheckPlaySound _GBA_P_WAV[i]
	  ClearPicture
	  If (B_FLG = "背面") Then
	    PaintPicture _SetBmpNum2(_GBA_P_NAME2,(_GBA_P_STR2 + ((k - 1) * _GBA_P_FRC2)),_GBA_P_TYPE2) _
	                 G_X2 G_Y2 G_SX2 G_SY2 DRCT2 透過 T_COL_F UD_OPT2 LR_OPT2 右回転 ANG2
	    PaintPicture _SetBmpNum2(_GBA_P_NAME,(_GBA_P_STR + ((i - 1) * _GBA_P_FRC)),_GBA_P_TYPE) _
	                 G_X G_Y G_SX G_SY DRCT 透過 T_COL_F UD_OPT LR_OPT 右回転 ANG
	    PaintPicture BMP_FILE C_X C_Y 32 32 透過 T_COL
	  Else
	    PaintPicture _SetBmpNum2(_GBA_P_NAME2,(_GBA_P_STR2 + ((k - 1) * _GBA_P_FRC2)),_GBA_P_TYPE2) _
	                 G_X2 G_Y2 G_SX2 G_SY2 DRCT2 透過 T_COL_F UD_OPT2 LR_OPT2 右回転 ANG2
	    PaintPicture _SetBmpNum2(_GBA_P_NAME,(_GBA_P_STR + ((i - 1) * _GBA_P_FRC)),_GBA_P_TYPE) _
	                 G_X G_Y G_SX G_SY DRCT 透過 T_COL_F UD_OPT LR_OPT 右回転 ANG
	  EndIf
	  Refresh
	  Wait Until (i * W_T)
	Next
Next

_GBA_UnSetBmpPattern


If (_S_OPT_FA("保持") = "保持") Then
  Return "Keep"
Else
  Return
EndIf


#_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/




戦闘アニメ_SCOTT_バックジャンプ準備:
戦闘アニメ_SCOTT_バックジャンプ攻撃:
戦闘アニメ_SCOTT_バックジャンプ命中:
# 他引数は「ジャンプ」に準じる
# 引数 垂直 で垂直ジャンプ 正 で前方ジャンプ
# 引数 速 遅
# 引数 上下反転
# 引数 回転 or 昇龍
# 引数 旋回音
# 引数 アッパー or 振り下ろし
# 引数 小移動 (x方向への移動量が半分になる)
# 引数 高

Local UID = _GetName(対象ユニットＩＤ,_GetIDFromArgs())
_CenteringCheck UID
Local C_X = WX(UID)
Local C_Y = WY(UID)
Local i L_NUM
Local BMP_FILE = _GetBMPName(UID)
Local BMP_FILE2 = _GetTileName(UID)
Local BMP_FILE3
Local ANG = _GetAngle(UID)
Local TYPE = Args(1)
Local X_F
Local Y_F
Local DRCT
Local DRCT2
Local DRCT3
Local W_T
Local UDR
Local FJ
Local R_S
Local UPE

_UnSetBattleAnimeValue

  Set L_NUM 5
  Set X_F _GetCircumference(ANG,Ｘ)
  Set Y_F 1

If (X(UID) < _Get_X(相手ユニットＩＤ)) Then
  Set DRCT 左右反転
  Set DRCT2 左右反転
  Set DRCT3 左右反転
  Set ANG 270
Else
  Set DRCT ""
  Set DRCT2 ""
  Set DRCT3 ""
  Set ANG 90
EndIf

If (_SearchOptionFromArgs("垂直") != "") Then
  Set X_F 0
EndIf

Set W_T 1
If (_SearchOptionFromArgs("速") != "") Then
  Set W_T 0.5
ElseIf (_SearchOptionFromArgs("遅") != "") Then
  Set W_T 1.5
EndIf

Set UDR ""
If (_SearchOptionFromArgs("上下反転") != "") Then
  Set UDR 上下反転
EndIf

# 方向判定
Set FJ 1
If (_SearchOptionFromArgs("正") != "") Then
  Set FJ -1
EndIf

If (_SearchOptionFromArgs("小移動") != "") Then
  FJ = (FJ * 0.5)
EndIf

If (_SearchOptionFromArgs("高") != "") Then
  Y_F = (Y_F * 2)
EndIf

If (_SearchOptionFromArgs("回転") != "") Then
  If DRCT2 = 左右反転 Then
  	  Set DRCT2 ""
  	  Set DRCT3 ""
  Else
  	  Set DRCT2 左右反転
  	  Set DRCT3 左右反転
  EndIf
EndIf

If (_SearchOptionFromArgs("昇龍") != "") Then
  If DRCT3 = 左右反転 Then
  	  Set DRCT3 ""
  Else
  	  Set DRCT3 左右反転
  EndIf
EndIf

# アッパー・振り下ろしエフェクト指定
Set BMP_FILE3 "-"
Set UPE ""
If (_SearchOptionFromArgs("アッパー") != "") Then
  Set BMP_FILE3 "Attack\EFFECT_Swing0"
  Set UPE 上下反転
ElseIf (_SearchOptionFromArgs("振り下ろし") != "") Then
  Set BMP_FILE3 "Attack\EFFECT_Swing0"
EndIf

Wait Start

ChangeUnitBitmap UID 非表示

For i = 1 To L_NUM
  ClearPicture
  _PaintTile BMP_FILE2 C_X C_Y

  Switch i
  Case 1
      PaintPicture BMP_FILE (C_X + (X_F * 12 * FJ)) C_Y 透過 DRCT2 UDR _ColorTime()
      PaintPicture (BMP_FILE3 & "1.bmp") (C_X + (X_F * 12 * FJ)) C_Y 透過 DRCT UPE _ColorTime()
  Case 2
      PaintPicture BMP_FILE (C_X + (X_F * 8 * FJ)) (C_Y + (Y_F * -4))  透過 DRCT UDR _ColorTime()
      PaintPicture (BMP_FILE3 & "2.bmp") (C_X + (X_F * 8 * FJ)) (C_Y + (Y_F * -4)) 透過 DRCT UPE _ColorTime()
  Case 3
    _SelectSound "Swing.wav" _GetWavFromArgs()
      PaintPicture BMP_FILE (C_X + (X_F * 4 * FJ)) (C_Y + (Y_F * -12)) 透過 DRCT2 UDR _ColorTime()
      PaintPicture (BMP_FILE3 & "3.bmp") (C_X + (X_F * 4 * FJ)) (C_Y + (Y_F * -12)) 透過 DRCT UPE _ColorTime()
  Case 4
      PaintPicture BMP_FILE C_X (C_Y + (Y_F * -16)) 透過 DRCT UDR _ColorTime()
      PaintPicture (BMP_FILE3 & "4.bmp") C_X (C_Y + (Y_F * -16)) 透過 DRCT UPE _ColorTime()
  Case 5
      PaintPicture BMP_FILE (C_X + (X_F * -8 * FJ)) (C_Y + (Y_F * -8)) 透過 DRCT3 UDR _ColorTime()
      PaintPicture (BMP_FILE3 & "4.bmp") (C_X + (X_F * -8 * FJ)) (C_Y + (Y_F * -8)) 透過 DRCT UPE _ColorTime()
  EndSw
  Refresh
  
	Incr R_S 1
  
	If R_S = 1 Then
  		If (_SearchOptionFromArgs("旋回音") != "") Then
  			_SelectSound "Swing.wav" _GetWavFromArgs()
  			Set R_S -1
  		EndIf
	EndIf

  Wait Until (i * 0.9 * W_T)
Next
Wait Until (((L_NUM * 0.9) + 0.4) * W_T)

  _SetAnimeData_UnitPoint (X_F * 8) (Y_F * 8) 0 DRCT "" "" UID

ChangeUnitBitmap UID 非表示解除

Return "Keep"



#_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
戦闘アニメ_SCOTT_スーパーキック改攻撃:
# 「餓狼伝説」の「スーパーキック改」を改変させていただきました。
#引数１ オーラの色
#引数３ 効果音

Local i
Local j
Local A_X
Local A_X
Local X_F
Local X_D
Local Y_F
Local ANG
Local DRC
Local BMP_FILE
Local BMP_FILE2
Local EFFECT_FILE
Local S_D
Local C_X
Local C_Y
Local UID
Local Time

_UnSetBattleAnimeValue

Set UID _GetName(対象ユニットＩＤ,_GetIDFromArgs())
Center UID
Set C_X WX(UID)
Set C_Y WY(UID)


Set EFFECT_FILE _GetBeamColor(_GetColorFromArgs())
If (EFFECT_FILE != "") Then
  Set EFFECT_FILE ("Common\EFFECT_SonicBoom" & EFFECT_FILE & "02.bmp")
EndIf

Set ANG _GetAngle(UID)
Set BMP_FILE _GetBMPName(UID)
Set BMP_FILE2 _GetTileName(UID)

If (X(UID) < _Get_X(相手ユニットＩＤ)) Then
  Set X_D -1
  Set DRC 左右反転
Else
  Set X_D 1
  Set DRC ""
EndIf

Wait Start

_SelectSound Whiz.wav _GetWavFromArgs()
If (BMP_FILE2 = "") Then
  ChangeUnitBitmap UID 非表示
EndIf

 Set Time 0.6

For i = 1 To 5
  ClearPicture
  If (BMP_FILE2 != "") Then
    PaintPicture BMP_FILE2 C_X C_Y _ColorTime()
  EndIf
  Switch i
  Case 1
    Set Y_F 8
  Case 2
    Set Y_F -32
  Case 3
    Set Y_F -48
  Case 4
    Set Y_F -56
  EndSw
  PaintPicture BMP_FILE C_X (C_Y + Y_F) 透過 DRC _ColorTime()
  Refresh
  Wait Until (i * Time)
Next


Center _Get_X(相手ユニットＩＤ) _Get_Y(相手ユニットＩＤ)
Set A_X WX(UID)
Set A_Y WY(UID)


Wait Start
For i = 1 To 3
  ClearPicture
  If (BMP_FILE2 != "") Then
    PaintPicture BMP_FILE2 A_X A_Y _ColorTime()
  EndIf
  Switch i
  Case 1
    Set X_F 48
    Set Y_F -48
  Case 2
    Set X_F 36
    Set Y_F -36
  Case 3
    Set X_F 24
    Set Y_F -24
  EndSw

  PaintPicture BMP_FILE (C_X + (X_F * X_D)) (C_Y + Y_F) 透過 DRC 右回転 (45 * X_D) _ColorTime()
  If (EFFECT_FILE != "") Then
    PaintPicture EFFECT_FILE (C_X + ((X_F - 4) * X_D)) (C_Y + Y_F + 4) 透過 DRC 右回転 (315 * X_D) _ColorTime(1)
  EndIf
  Refresh
  Wait Until (i * 0.4)
Next

_SetAnimeData_UnitPoint2 (X_F * X_D) Y_F (45 * X_D) DRC "" "" UID
Set 戦闘アニメ変数[色] _GetBeamColor(_GetColorFromArgs())

If (BMP_FILE2 = "") Then
  ChangeUnitBitmap UID 非表示解除
EndIf

Return "Keep"


#_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

戦闘アニメ_SCOTT_背向け準備:
戦闘アニメ_SCOTT_背向け攻撃:
戦闘アニメ_SCOTT_背向け命中:
# 引数 背を向けている時間(必須)
# 引数 効果音
# 引数 ユニットＩＤ
# 引数 倒れ
# 引数 上下反転

Local UID
UID = _GetName(対象ユニットＩＤ,_GetIDFromArgs())

_CenteringCheck UID
Local C_X = _Get_WX(UID)
Local C_Y = _Get_WY(UID)

Local W_T
Local BMP_FILE = _GetBMPName(UID)
Local BMP_FILE2 = _GetTileName(UID)
Local UDR


Local ANGL = 0
If (_SearchOptionFromArgs("倒れ") != "") Then
  Set ANGL 90
EndIf

Set UDR ""
If (_SearchOptionFromArgs("上下反転") != "") Then
  Set UDR 上下反転
EndIf

Set W_T _GetNumberFromArgs()

_SelectSound "Swing.wav" _GetWavFromArgs()

ClearPicture
  _PaintTile BMP_FILE2 C_X C_Y
  PaintPicture BMP_FILE C_X C_Y 透過 左回転 ANGL 左右反転 UDR
Refresh
Wait W_T
ClearPicture
Refresh

Return
