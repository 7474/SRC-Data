#強制合体解除インクルード
#　このインクルードは、獣士ナマズンゴ、ナマズンゴＳ、カニガンが装備しているアンチ超電磁波が
#合体ロボに対してクリティカルしたとき強制的に分離させる（ハイパーモードで合体を代用する
#勇者ロボの場合はハイパーモードを強制解除する）ものです。
#強大なパワーを誇る合体ロボたちの天敵が欲しいときなどにご利用下さい


#使い方
#シナリオ中に以下のように組み込みます。（自動インクルードではないのでシナリオ側で指定しないと
#動作しません
#SRC1.6.37以降専用です。それ以前のバージョンでは正しく動作しません。

#<Data\超電磁マシーンボルテスⅤ\合体崩し.eve>
#Set 分離対象 0

#　変数「分離対象」が0ボルテスＶのみが強制分離の対象になります。1だとボルテスⅤと
#コン・バトラーＶ、2だと全てのロボ(大空魔竜、ダンクーガＡを除く）強制分離の対象になります。
#　また合体を解除する武器をアンチ超電磁波以外の武器（相手を行動不能にするものに限る）に
#変更することもシナリオ側からの操作で一応可能です。例えばナルマグネ光線を合体解除武器に
#したい場合は、プロローグイベント、またはスタートイベントで

#Set アンチ超電磁波 ナルマグネ光線

#と指定して下さい。

#注意事項
#拡張データには対応していません。
#Attackコマンドによる攻撃では合体解除は発生しません。
#パーツ合体ユニットに対しては強制分離は起きません（ゲーム上の措置として意図的に対象から
#外してあります）
#大空魔竜は強制分離しません。（ビームを喰らったらガイキングが発進するというのも変な話なので）
#自発的な分離が出来ない状態（分離形態が生成されていないなど）の場合は強制分離が起きません。




#変数をローカルに
Local 電磁標的 検索対象 判別値 変形取得

#使用者、標的の識別
使用 敵 アンチ超電磁波:
使用 味方 アンチ超電磁波:
使用 中立 アンチ超電磁波:
使用 ＮＰＣ アンチ超電磁波:
Set 電磁標的 相手ユニット
 If IsEquiped(電磁標的,ウルトラマグコン) = 1 Then
 SetStatus 電磁標的 無敵 1
 Endif
 If IsEquiped(電磁標的,固定型ウルトラマグコン) = 1 Then
 SetStatus 電磁標的 無敵 1
 Endif
Exit

##########
#分離判定への誘導
使用後 敵 アンチ超電磁波:
使用後 味方 アンチ超電磁波:
使用後 中立 アンチ超電磁波:
使用後 ＮＰＣ アンチ超電磁波:
ClearStatus 電磁標的 無敵
If Condition(電磁標的,行動不能) Then
Call "超電磁破壊判定"
Endif
Exit

########
#分離判定＆変数リセット
超電磁破壊判定:

#分離対象チェック

If Info(電磁標的, 機体クラス) = "大空魔竜" Then
Return
Endif
If Unit(電磁標的) = ボルテスⅤ Then 
Goto "分離チェック"
Endif
Switch 分離対象
Case 1
 If Unit(電磁標的) = コン・バトラーＶ Then
Goto "分離チェック"
Endif
If Unit(電磁標的) = コン・バトラーＶ(前期型)  Then
Goto "分離チェック"
Else
Return
Endif
Case 2
If IsAvailable(電磁標的,分離) = 1 Then
Call "分離チェック"
Endif
If IsAvailable(電磁標的,ノーマルモード) = 1 Then
Goto "勇者分離"
Else
Return
Endif
EndSw
Return

分離チェック:

ClearStatus 電磁標的 行動不能
Split 電磁標的
Unset 電磁標的 
Return

勇者分離:
UnSet 判別値
Set 検索対象 UnitID(電磁標的)
Set 機体クラス Info(検索対象,機体クラス)
#For I = 1 To Len(機体クラス)
#If Mid(機体クラス,I,1) = "(" Then
#Break
#Else
#Set 判別値 ( 判別値 & Mid(機体クラス,I,1) )
#Endif
#Next I
If Instr(機体クラス,"(") <  Then
Set 判別値 機体クラス
Else
Set 判別値 Left(機体クラス,(InStr(機体クラス,"(")) - 1)
Endif
Switch 判別値
Case レジェンドラの勇者 伝説の勇者 宇宙警察カイザーズ スーパーメカノイド 
Goto "通常勇者処理"
Case ビークルロボ ブレイブポリス 宇宙警察機構ロボ 聖勇者 ブラックガイン
Goto "通常勇者処理"
Case 宇宙警備隊メカ
Goto "特殊勇者処理"
EndSw
return

通常勇者処理:
UnSet 判別値
UnSet 機体クラス
Set 変形取得 Info(検索対象,特殊能力データ,ノーマルモード)
Transform 電磁標的 変形取得
Set 電磁標的 (pilot(変形取得))
ClearStatus 電磁標的 行動不能
ClearStatus 電磁標的 消耗
Unset 電磁標的 
Return

特殊勇者処理:
UnSet 判別値
UnSet 機体クラス
Switch Unit(検索対象)
Case グレートファイバード 武装合体ファイバード
Transform 検索対象 ファイバード
Set 電磁標的 (pilot(ファイバード))
ClearStatus 電磁標的 行動不能
Case ジェット合体グランバード 
Transform 検索対象 グランバード
Set 電磁標的 (pilot(グランバード))
ClearStatus 電磁標的 行動不能
ClearStatus 電磁標的 消耗
Case ガードスター(メットオン) ガードファイアー(メットオン) ガードレスキュー(メットオン) 
Return
EndSw
Return

#ファイバードのメカは通常勇者処理だと、グレートファイバードが一気に火鳥になってしまう、
#ガードチームのメットを脱がしてしまう等の問題が多いので個別に処理しています。

#更新履歴
#2001.11.19
#SRC本体仕様変更に伴い対象ユニット、相手ユニットの扱いが変化したため改訂